plugins {
  id 'com.jfrog.artifactory' version '4.9.8'
  id 'org.javamodularity.moduleplugin' version '1.6.0'
  id 'idea'
  id 'java'
  id 'maven'
}

defaultTasks 'build'

sourceCompatibility = '1.9'

repositories {
  mavenCentral()
  jcenter()
  mavenLocal()
}

ext {
  isSnapshot = true
  hktBaseVersion = '0.10.0'
  hktVersion = hktBaseVersion + (isSnapshot ? '-SNAPSHOT' : '')

  autoService = 'com.google.auto.service:auto-service:1.0-rc6'
  derive4jVersion = '1.1.1'
  derive4jAnnotation = "org.derive4j:derive4j-annotation:$derive4jVersion"
}

group 'org.derive4j.hkt'
version hktVersion

dependencies {
  compileOnly derive4jAnnotation
  compileOnly autoService

  annotationProcessor "org.derive4j:derive4j:$derive4jVersion"
  annotationProcessor autoService

  testCompile 'junit:junit:4.12'
  testCompile('com.google.testing.compile:compile-testing:0.18') {
    exclude module: 'auto-value'
  }
  testCompileOnly derive4jAnnotation
  testCompileOnly autoService

  testAnnotationProcessor rootProject
  testImplementation rootProject
}

test {
  testLogging.showStandardStreams = true
  moduleOptions {
    runOnClasspath = true
  }
}

sourceSets {
  generated {
    java.srcDir "$buildDir/generated/sources/annotationProcessor/java/main"
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << '-Xlint:all'
  options.encoding = "UTF-8"
}

task myJavadocs(type: Javadoc) {
  source = sourceSets.main.java + sourceSets.generated.java
  classpath = sourceSets.main.compileClasspath
  doFirst {
    options.addStringOption('-module-path', classpath.asPath)
  }
  verbose = true
}

task javadocJar(type: Jar, dependsOn: 'myJavadocs') {
  archiveClassifier = 'javadoc'
  from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
  archiveClassifier = 'sources'
  from sourceSets.main.allSource + sourceSets.generated.java
}

artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}

install {
  repositories.mavenInstaller {
    pom {
      groupId = project.group
      project {
        name = 'HKT'
        packaging 'jar'
        description 'Interfaces and type-checker for higher kinded types support in Java'
        url 'https://github.com/derive4j/hkt'
        organization {
          name = 'Derive4J HKT contributors'
        }
        scm {
          url 'git://github.com/derive4j/hkt.git'
        }
        licenses {
          license {
            name = 'BSD-3'
            url 'https://github.com/derive4j/hkt/blob/master/LICENSE'
            distribution 'repo'
          }
        }
        developers {
          developer {
            name = 'GrÃ©goire Neuville'
          }
          developer {
            name = 'Jean-Baptiste Giraudeau'
            email 'jb@giraudeau.info'
          }
        }
      }
    }
  }
}

artifactoryPublish {
  dependsOn sourcesJar, javadocJar
}

artifactory {
  contextUrl = 'http://oss.jfrog.org/artifactory'   //The base Artifactory URL if not overridden by the publisher/resolver
  publish {
    repository {
      // The Artifactory repository key to publish to :
      if (isSnapshot) {
        repoKey = 'oss-snapshot-local'
      } else {
        repoKey = 'oss-release-local'
      }
      username = jfrogUsername         //The publisher user name
      password = jfrogPassword      //The publisher password
    }
    defaults {
      publishConfigs('archives')
    }
  }
  resolve {
    repository {
      repoKey = 'libs-releases'  //The Artifactory (preferably virtual) repository key to resolve from
    }
  }
}
